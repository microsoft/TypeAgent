// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.

// Grammar rules for Local Music Player

@ <Start> = <Pause>
          | <Resume>
          | <Stop>
          | <Next>
          | <Previous>
          | <Status>
          | <PlayCommand>
          | <VolumeCommand>
          | <Mute>
          | <Unmute>
          | <ShowQueue>
          | <ClearQueue>

@ <Pause> = pause ((the)? music)?   ->  { actionName: "pause" }

@ <Resume> = resume ((the)? music)?   ->  { actionName: "resume" }

@ <Stop> = stop ((the)? music)?   ->  { actionName: "stop" }

@ <Next> = (next | skip) (track | song)?   ->  { actionName: "next" }

@ <Previous> = (previous | back | last) (track | song)?   ->  { actionName: "previous" }

@ <Status> = (what('s | is) | show) (playing | status | now playing)?   ->  { actionName: "status" }

@ <PlayCommand> = <PlayTrackNumberCommand>

@ <PlayTrackNumberCommand> =
    play (the)? $(n:<Ordinal>) (track | song)? -> {
        actionName: "playFromQueue",
        parameters: {
            trackNumber: $(n)
        }
    }
| play track $(n:<Cardinal>) -> {
        actionName: "playFromQueue",
        parameters: {
            trackNumber: $(n)
        }
   }
| play track #$(n:number) -> {
        actionName: "playFromQueue",
        parameters: {
            trackNumber: $(n)
        }
   }

@ <VolumeCommand> = <VolumeUp> | <VolumeDown> | <SetVolume>

@ <LocalPlayerPrefix> = (local player | local music player | music player)?

@ <VolumeUp> = <LocalPlayerPrefix> (turn up | increase | raise) (the)? volume
    -> { actionName: "changeVolume", parameters: { amount: 10 } }

@ <VolumeDown> = <LocalPlayerPrefix> (turn down | decrease | lower) (the)? volume
    -> { actionName: "changeVolume", parameters: { amount: -10 } }

@ <SetVolume> = <LocalPlayerPrefix> set volume (to)? $(n:number) (percent)?
    -> { actionName: "setVolume", parameters: { level: $(n) } }

@ <Mute> = mute (the)? (music | sound | audio)?   ->  { actionName: "mute", parameters: { isMuted: true } }

@ <Unmute> = unmute (the)? (music | sound | audio)?   ->  { actionName: "mute", parameters: { isMuted: false } }

@ <ShowQueue> = (show | list | display) (the)? queue   ->  { actionName: "showQueue" }

@ <ClearQueue> = clear (the)? queue   ->  { actionName: "clearQueue" }

@ <Cardinal> = 
  $(x:number) 
|   one -> 1
|   two -> 2
|   three -> 3
|   four -> 4
|   five -> 5
|   six -> 6
|   seven -> 7
|   eight -> 8
|   nine -> 9
|   ten -> 10

@ <Ordinal> = 
    first -> 1
|   second -> 2
|   third -> 3
|   fourth -> 4
|   fifth -> 5
|   sixth -> 6
|   seventh -> 7
|   eighth -> 8
|   ninth -> 9
|   tenth -> 10
