// Copyright (c) Microsoft Corporation.
// Licensed under the MIT License.

@ <Start> = <Pause>
          | <Resume>
          | <Next>
          | <Previous>
          | <PlayCommand>
@ <Pause> = pause ((the)? music)?   ->  { actionName: "pause" }
@ <Resume> = resume ((the)? music)?   ->  { actionName: "resume" }
@ <Next> = (<PlayTrackAction>)? (the)? next (<Item>)? -> { actionName: "next" }
@ <Previous> = (<PlayTrackAction>)? (the)? previous (<Item>)? -> { actionName: "previous" }

@ <PlayCommand> = <PlayTrackNumberCommand> | <PlaySpecificTrack>
@ <PlayTrackNumberCommand> =
    <PlayTrackAction> (the)? $(n:<Ordinal>) (<Item>)? -> {
        actionName: "playFromCurrentTrackList",
        parameters: {
            trackNumber: $(n)
        }
    }
| <PlayTrackAction> (<TrackTerm>)? (<NumberIndicator>)? $(n:<Cardinal>) -> {
        actionName: "playFromCurrentTrackList",
        parameters: {
            trackNumber: $(n)
        }
   }
| <PlayTrackAction> (<TrackTerm>)? (<NumberIndicator>)? $(n:number) -> {
        actionName: "playFromCurrentTrackList",
        parameters: {
            trackNumber: $(n)
        }
   }
@ <PlayTrackAction> = play | joue | cue up
@ <NumberIndicator> = # | numero | number | no.
@ <Item> = one | cut | <TrackTerm> 
@ <TrackTerm> = track | song

@ <Cardinal> = 
  $(x:number) 
|   one -> 1
|   two -> 2
|   three -> 3
|   four -> 4


@ <Ordinal> = 
    first -> 1
|   second -> 2
|   third -> 3
|   troisiemme -> 3
|   fourth -> 4

@ <TrackPhrase> = ((the)? <TrackTerm>)? $(trackName:<TrackName>)
                | $(trackName:<TrackName>) <TrackTerm>
@ <PlaySpecificTrack> = play $(trackName:<TrackPhrase>) -> { actionName: "playTrack", parameters: { trackName: $(trackName) } }
                      | play $(trackName:<TrackPhrase>) by $(artist:<ArtistName>) -> { actionName: "playTrack", parameters: { trackName: $(trackName), artists: [$(artist)] } }
                      | play $(trackName:<TrackPhrase>) from (the)? album $(albumName:<AlbumName>) -> { actionName: "playTrack", parameters: { trackName: $(trackName), albumName: $(albumName) } }
                      | play $(trackName:<TrackPhrase>) by $(artist:<ArtistName>) from (the)? album $(albumName:<AlbumName>) -> { actionName: "playTrack", parameters: { trackName: $(trackName), artists: [$(artist)], albumName: $(albumName) } }                      

// TODO: wire up entity names
@ <TrackName> = $(x:string)
@ <ArtistName> = $(x:string)  
@ <AlbumName> = $(x:string)


